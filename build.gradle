buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.25')
    }
}

plugins {
    id 'java'
    // Apply the application plugin to add support for building a CLI application
    id 'application'
    id 'jacoco'
    //id 'com.github.sherter.google-java-format' version '0.8'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'io.freefair.lombok' version '5.3.0'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'org.checkerframework' version '0.5.12'
    id 'org.sonarqube' version '3.0'
    id 'org.springframework.boot' version '2.3.6.RELEASE'
}

apply plugin: 'io.spring.javaformat'

sourceCompatibility = 11

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'ch.qos.logback:logback-classic:1.2.+'
    implementation 'org.apache.commons:commons-csv:1.8'
    implementation 'org.apache.commons:commons-lang3:3.11'
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:30.0-jre'

    runtime 'org.codehaus.groovy:groovy:3.0.+'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
}

group 'com.github.wdstar'
version = {
    def tags = grgit.tag.list().collect { it.getName() }
    def versionStr
    if (tags.any { it ==~ /v\d.*/ }) {
        versionStr = grgit.describe(longDescr: false, tags: true, match: ['v[0-9]*'])
    } else {
        versionStr = grgit.head().abbreviatedId
    }
    return versionStr.replaceFirst(/^v/, '') + (grgit.status().isClean() ? '' : '+dirty')
}()

task showVersion {
    doLast {
        println version
    }
}

application {
    // Define the main class for the application
    mainClassName = 'com.github.wdstar.java.cli.Example'
}

checkerFramework {
    excludeTests = true
    checkers = [
        'org.checkerframework.checker.formatter.FormatterChecker',
        'org.checkerframework.checker.nullness.NullnessChecker',
        'org.checkerframework.checker.regex.RegexChecker',
        'org.checkerframework.checker.signature.SignatureChecker',
    ]
}

test {
    testLogging {
        events 'passed', 'failed', 'skipped'
    }
}

def jarExcludes = [
    'META-INF/INDEX.LIST',
    'META-INF/*.SF',
    'META-INF/*.DSA',
    'META-INF/*.RSA'
]

jar {
    // By default, when the bootJar or bootWar tasks are configured, the jar or war tasks are disabled.
    // A project can be configured to build both an executable archive and a normal archive at the same time
    // by enabling the jar or war task:
    enabled = true
    manifest {
        attributes 'Manifest-Version': '0.1.0'
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': version
        attributes 'Main-Class': application.mainClassName
    }
    
    version version
    // Note: this fat jar excludes any JAR index or cryptographic signature files like the Shadow plugin.
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it).matching { exclude jarExcludes } }
    from configurations.runtime.collect { it.isDirectory() ? it : zipTree(it).matching { exclude jarExcludes } }
}

// nested jar generation task
bootJar {
    manifest {
        attributes 'Manifest-Version': '0.1.0'
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': version
    }
    
    mainClassName = application.mainClassName
}

// uber jar generation task
shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

// for `report` task of Jacoco with PowerMock
apply from: 'gradle/jacoco-offline-instrument.gradle'

project.tasks['sonarqube'].dependsOn 'report'
sonarqube {
    properties {
        property 'sonar.projectKey', 'wdstar_java-cli-example'
        property 'sonar.organization', 'wdstar'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.jacoco.reportPath', report.jacocoDestFile
    }
}
